# -*- Python -*- (syntax highlighting)
# ----------------------------------------------------------------------
#
# Brad T. Aagaard, U.S. Geological Survey
# Charles A. Williams, GNS Science
# Matthew G. Knepley, University at Buffalo
#
# This code was developed as part of the Computational Infrastructure
# for Geodynamics (http://geodynamics.org).
#
# Copyright (c) 2010-2022 University of California, Davis
#
# See LICENSE.md for license information.
#
# ----------------------------------------------------------------------
#
# CUBIT journal file with geometry for 2-D subduction zone example
# based on the 2011 M9.0 Tohoku earthquake.
#
# We use the CUBIT support for APREPRO (the expressions in braces),
# which includes units and mathematical functions. See the APREPRO
# section in the appendix of the CUBIT documentation for more
# information.
#
# The steps in constructing the geometry are:
# (1) Create points
# (2) Connect the points into spline curves
# (3) Split the splines at intersections to form bounding curves
# (4) Form surfaces from the bounding curves
#
# Points have been projected from longitude/latitude into a local
# transverse Mercator projection. PyLith uses the Proj.4 library
# for geographic projections. The proj parameters are:
#
#   +proj=tmerc +datum=WGS84 +lon_0=142.0 +lat_0=38.0 +k=0.9996
#
# so that the local origin is at a longitude of 142.0 degrees (WGS84)
# and a latitude of 38.0 degrees (WGS84).
#
# ----------------------------------------------------------------------
# Set units to SI.
# ----------------------------------------------------------------------
${Units('si')}
#
# ----------------------------------------------------------------------
# Reset geometry.
# ----------------------------------------------------------------------
reset

# Make sure undo is off to prevent errors in stitching volumes.
undo off

# ----------------------------------------------------------------------
# Create points and curves defining materials
# ----------------------------------------------------------------------

# Topography/bathymetry (points from Google Earth)
#
# Save ids of important points in APREPRO variables as they are created
# for use in other commands. We save the entity id in a variable rather
# than name the vertex because this allows us to still use "idA to idB"
# to select multiple vertices.
#
create vertex x { -600.0*km} y {-2000.0*m}
${p_topo_west=Id("vertex")}
create vertex x { -439.1*km} y { -300.0*m}
create vertex x { -351.2*km} y { -800.0*m}
create vertex x { -263.4*km} y {    0.0*m}
create vertex x { -175.6*km} y {  400.0*m}
create vertex x {  -87.7*km} y {    0.0*m}
create vertex x {    0.0*km} y { -400.0*m}
create vertex x {   87.7*km} y {-3000.0*m}
create vertex x {  165.6*km} y {-6000.0*m}
${p_topo_trench=Id("vertex")}
create vertex x {  263.4*km} y {-5400.0*m}
create vertex x {  351.2*km} y {-5400.0*m}
create vertex x {  439.1*km} y {-5400.0*m}
create vertex x {  600.0*km} y {-5700.0*m}
${p_topo_east=Id("vertex")}

create curve spline vertex {p_topo_west} to {p_topo_east}
curve {Id("curve")} name "c_topo"

# Top of slab
# Hayes and Wald, 2009
# http://earthquake.usgs.gov/research/data/slab
create vertex x { -422.4*km} y {-240.00*km}
${p_slabtop_west=Id("vertex")}
create vertex x { -331.0*km} y {-180.00*km}
create vertex x { -261.6*km} y {-140.00*km}
create vertex x { -223.9*km} y {-120.00*km}
create vertex x { -182.6*km} y {-100.00*km}
create vertex x { -134.3*km} y {-80.00*km}
create vertex x {  -74.6*km} y {-60.00*km}
${p_slabtop_coseismic=Id("vertex")}
create vertex x {   -7.9*km} y {-40.00*km}
${p_slabtop_moho=Id("vertex")}
create vertex x {   71.1*km} y {-20.00*km}
create vertex x {  160.5*km} y {-7.50*km}
${p_slabtop_u=Id("vertex")}

create curve spline vertex {p_slabtop_west} to {p_slabtop_u} {p_topo_trench}
curve {Id("curve")} name "c_slabtop"

# Bottom of slab (translate top of slab to the east)
#
# Better approach would be to move points normal to slab to preserve
# uniform thickness.
vertex {p_slabtop_west} to {p_slabtop_coseismic} copy move X {120.0*km}
${p_slabbot_west=p_slabtop_u+1}
create vertex x {  175.6*km} y {-40.0*km}
create vertex x {  600.0*km} y {-40.0*km}
${p_slabbot_east=Id("vertex")}

create curve spline vertex {p_slabbot_west} to {p_slabbot_east}
curve {Id("curve")} name "c_slabbot"

# Bottom edge of slab
create curve spline vertex {p_slabtop_west} {p_slabbot_west}
curve {Id("curve")} name "c_slab_end"

# Top of mantle (uniform depth of 40 km)
create vertex x { -600.0*km} y {-40.00*km}
${p_moho_west=Id("vertex")}

create curve spline vertex {p_moho_west} {p_slabtop_moho}
curve {Id("curve")} name "c_conmoho"

# Lateral edges and bottom boundary
create vertex x { -600.0*km} y {-600.00*km}
${p_bot_west=Id("vertex")}

create vertex x { 600.0*km} y {-600.00*km}
${p_bot_east=Id("vertex")}

create curve spline vertex {p_topo_west} {p_moho_west} {p_bot_west}
curve {Id("curve")} name "c_west"

create curve spline vertex {p_bot_west} {p_bot_east}
curve {Id("curve")} name "c_bot"

create curve spline vertex {p_topo_east} {p_slabbot_east} {p_bot_east}
curve {Id("curve")} name "c_east"

# ----------------------------------------------------------------------
# Split curves to form bounding curves for each material
#
# Constructing the entire boundary curves as splines and then breaking
# them into pieces bounding the surfaces preserves continuity in slip.
# ----------------------------------------------------------------------
split curve c_topo crossing curve c_slabtop
split curve c_slabtop crossing curve c_conmoho
split curve c_west crossing curve c_conmoho
split curve c_east crossing curve c_slabbot

# ----------------------------------------------------------------------
# Create surfaces using bounding curves
# ----------------------------------------------------------------------
# Continental crust
create surface curve c_topo c_west c_conmoho c_slabtop@A
surface {Id("surface")} name "concrust"

# Oceanic crust (slab)
create surface curve c_topo@A c_slabtop@A c_slabtop c_slab_end c_slabbot c_east
surface {Id("surface")} name "oceancrust"

# mantle
create surface curve c_west@A c_bot c_east@A c_slabbot c_slab_end c_slabtop c_conmoho
surface {Id("surface")} name "mantle"

# ----------------------------------------------------------------------
# Imprint/merge
# ----------------------------------------------------------------------
delete vertex all
imprint all
merge all

# We must stitch the surfaces into a single volume in order to split
# the curves for the purpose of defining the discretization size along
# various portions of the curves.
stitch volume all

# Split top of slab for fault surface
split curve c_slabtop distance {80.0*km} from end

# Split topography/bathymetry to mimic same region as fault surface
# (used in setting discretization size)
split curve c_topo distance {190.0*km} from end

# Split bottom of slab to mimic same region as fault surface
# (used in setting discretization size)
split curve c_slabbot distance {420.0*km} from end
split curve c_slabbot distance {250.0*km} from end

# End of file



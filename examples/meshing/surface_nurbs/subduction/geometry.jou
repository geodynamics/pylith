# -*- Python -*- (syntax highlighting)
# ----------------------------------------------------------------------
#
# Brad T. Aagaard, U.S. Geological Survey
# Charles A. Williams, GNS Science
# Matthew G. Knepley, University of Chicago
#
# This code was developed as part of the Computational Infrastructure
# for Geodynamics (http://geodynamics.org).
#
# Copyright (c) 2010-2013 University of California, Davis
#
# See COPYING for license information.
#
# ----------------------------------------------------------------------
#
# CUBIT journal file with geometry for example showing how to import
# several surfaces (topography, subduction interface, splay fault), and
# mesh the resulting volumes.
#
# We also use the CUBIT support for APREPRO (the expressions in
# braces), which includes units and mathematical functions. See the
# APREPRO section in the appendix of the CUBIT documentation for more
# information.
#
# ----------------------------------------------------------------------
# Set units to SI.
# ----------------------------------------------------------------------
# {Units('si')}
#
# ----------------------------------------------------------------------
# Reset geometry.
# ----------------------------------------------------------------------
reset

# Make sure undo is off to prevent errors in stitching volumes.
undo off

# ----------------------------------------------------------------------
# Create block
# ----------------------------------------------------------------------
# Block is 250 km x 500 km x 62 km
#{blockLength=250.0*km}
#{blockWidth=500.0*km}
#{blockHeight=66.0*km}

brick x {blockLength} y {blockWidth} z {blockHeight}
#{idVol=Id("volume")}

# Translate block so top can be chopped with topography and so faults
# intersect upper surface rather than side.
#{moveX=50.0*km}
#{moveY=0.0*km}
#{moveZ=-30.0*km}

volume {idVol} move x {moveX} y {moveY} z {moveZ}

# ----------------------------------------------------------------------
# Import topography, subduction interface, and splay fault.
# ----------------------------------------------------------------------
import Acis "topobath_surf.sat"
import Acis "interface_surf.sat"
import Acis "splay_surf.sat"

# ----------------------------------------------------------------------
# Webcut block with topography and delete volume lying above topography.
# ----------------------------------------------------------------------
webcut volume {idVol} with sheet surface 7
delete volume 5

# ----------------------------------------------------------------------
# Webcut remaining portion of block with subduction interface.
# ----------------------------------------------------------------------
webcut volume 1 with sheet surface 8

# ----------------------------------------------------------------------
# Webcut volume above subduction interface with splay fault.
# ----------------------------------------------------------------------
webcut volume 1 with sheet surface 9

# ----------------------------------------------------------------------
# Delete sheet bodies, which are no longer needed.
# ----------------------------------------------------------------------
delete body 2 3 4

# ----------------------------------------------------------------------
# Imprint all volumes, then merge.
# ----------------------------------------------------------------------
imprint all with volume all
merge all

# End of file

# -*- Python -*- (syntax highlighting)
# ----------------------------------------------------------------------
#
# Brad T. Aagaard, U.S. Geological Survey
# Charles A. Williams, GNS Science
# Matthew G. Knepley, University of Chicago
#
# This code was developed as part of the Computational Infrastructure
# for Geodynamics (http://geodynamics.org).
#
# Copyright (c) 2010-2014 University of California, Davis
#
# See COPYING for license information.
#
# ----------------------------------------------------------------------
# Create tri3 mesh at 100m resolution.
# ----------------------------------------------------------------------

# ----------------------------------------------------------------------
# Generate geometry
# ----------------------------------------------------------------------
playback 'geometry.jou'

# ----------------------------------------------------------------------
# Set discretization size and scheme
# ----------------------------------------------------------------------
#{dxInner=150.0*m}
#{dxOuter=0.75*km}

surface all scheme trimesh
surface 23 size {dxInner}
curve 53 59 60 size {dxOuter}

# ----------------------------------------------------------------------
# Generate the mesh
# ----------------------------------------------------------------------
mesh surface all

# ----------------------------------------------------------------------
# Smooth mesh to improve quality
# ----------------------------------------------------------------------
surface all smooth scheme condition number beta 1.2 cpu 10
smooth surface all

surface all smooth scheme condition number beta 1.1 cpu 10
smooth surface all

# ----------------------------------------------------------------------
# Setup boundary conditions.
# ----------------------------------------------------------------------
block 1 surface 23
block 1 name 'elastic'
block 2 surface 24
block 2 name 'viscoelastic'

group "face_xpos" add node in curve 59
nodeset 11 group face_xpos
nodeset 11 name "face_xpos"

group "face_xneg" add node in curve 53
nodeset 12 group face_xneg
nodeset 12 name "face_xneg"

group "face_yneg" add node in curve 60
nodeset 13 group face_yneg
nodeset 13 name "face_yneg"

group "face_ypos" add node in curve 54 51 58
nodeset 14 group face_ypos
nodeset 14 name "face_ypos"

# ----------------------------------------------------------------------
# Export exodus file
# ----------------------------------------------------------------------
export mesh "mesh.exo" dimension 2 overwrite


# End of file





# -*- Python -*- (syntax highlighting)
# ----------------------------------------------------------------------
#
# Brad T. Aagaard, U.S. Geological Survey
# Charles A. Williams, GNS Science
# Matthew G. Knepley, University of Chicago
#
# This code was developed as part of the Computational Infrastructure
# for Geodynamics (http://geodynamics.org).
#
# Copyright (c) 2010-2015 University of California, Davis
#
# See COPYING for license information.
#
# ----------------------------------------------------------------------
# Set vertex spacing with increasing spacing away from fault
# ----------------------------------------------------------------------
${bias_factor=1.05}

# Function for computing discretization size at end of curve with bias
${fun_dx='dxStart*bias_factor**ceil( ln(1-curveL/dxStart*(1-bias_factor))/ln(bias_factor))'}

# ----------------------------------------------------------------------
# Compute sizes at curve endpoints
# ----------------------------------------------------------------------

# dxA - size at vertex_ypos_mid, vertex_yneg_mid
${dxStart=dx}{curveL=Length(Id("fault_ext_ypos"))}{execute("dxA="//fun_dx)}

# dxB - size at domain corners
${dxStart=dxA}{curveL=Length(Id("edge_ypos_xpos"))}{execute("dxC="//fun_dx)}

# ----------------------------------------------------------------------
# Reset sizes
# ----------------------------------------------------------------------
curve all scheme default
surface all sizing function none

# Set size on faults
curve fault_surface size {dx}

# Set bias on curves extending from faults
curve fault_ext_ypos scheme bias fine size {dx} factor {bias_factor} start vertex vertex_fault_ypos
curve fault_ext_yneg scheme bias fine size {dx} factor {bias_factor} start vertex vertex_fault_yneg

# A to B
curve edge_ypos_xneg scheme bias fine size {dxA} factor {bias_factor} start vertex vertex_ypos_mid
curve edge_ypos_xpos scheme bias fine size {dxA} factor {bias_factor} start vertex vertex_ypos_mid
curve edge_yneg_xneg scheme bias fine size {dxA} factor {bias_factor} start vertex vertex_yneg_mid
curve edge_yneg_xpos scheme bias fine size {dxA} factor {bias_factor} start vertex vertex_yneg_mid

# B to B
curve edge_xneg size {dxC}
curve edge_xpos size {dxC}

surface all sizing function type bias start curve fault_surface factor {bias_factor}

# End of file




# -*- Python -*- (syntax highlighting)

# ----------------------------------------------------------------------
# Set vertex spacing with increasing spacing away from fault
# ----------------------------------------------------------------------
${bias_factor=1.05}

# Function for computing discretization size at end of curve with bias
${fun_dx='dxStart*bias_factor**ceil( ln(1-curveL/dxStart*(1-bias_factor))/ln(bias_factor))'}

# ----------------------------------------------------------------------
# Compute sizes at curve endpoints
# ----------------------------------------------------------------------

# dxA - size at v_top_xpos
${dxStart=dx}{curveL=Length(Id("c_ypos_fw"))}{execute("dxA="//fun_dx)}

# dxB - size at v_top_xneg
${dxStart=dx}{curveL=Length(Id("c_ypos_hw"))}{execute("dxB="//fun_dx)}

# dxC - size at v_fault_xneg
${dxStart=dx}{curveL=Length(Id("c_fault_ext"))}{execute("dxC="//fun_dx)}

# dxD - size at v_bot_xpos
${dxStart=dxA}{curveL=Length(Id("c_xpos"))}{execute("dxD="//fun_dx)}

# ----------------------------------------------------------------------
# Reset sizes
# ----------------------------------------------------------------------
curve all scheme default
surface all sizing function none

# Set size on faults
curve c_fault_upper size {dx}
curve c_fault_lower size {dx}
curve c_splay size {dx}
curve c_ypos_w size {dx}

# Set bias on curves extending from faults
curve c_ypos_fw scheme bias fine size {dx} factor {bias_factor} start vertex v_fault_top
curve c_ypos_hw scheme bias fine size {dx} factor {bias_factor} start vertex v_splay_top
curve c_fault_ext scheme bias fine size {dx} factor {bias_factor} start vertex v_fault_bot

curve c_yneg size {dxD}

# A to D
curve c_xpos scheme bias fine size {dxA} coarse size {dxD} start vertex v_top_xpos

# C to B
curve c_xneg_hw scheme bias fine size {dxC} coarse size {dxB} start vertex v_fault_xneg

# B to D
curve c_xneg_fw scheme bias fine size {dxB} coarse size {dxD} start vertex v_fault_xneg

# For CUBIT 15.4
set trimesher surface gradation {bias_factor}

# For earlier versions of CUBIT
surface all sizing function type bias start curve c_fault_upper c_fault_lower c_splay c_ypos_w factor {bias_factor}


# End of file


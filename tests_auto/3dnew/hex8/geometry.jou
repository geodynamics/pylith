# ----------------------------------------------------------------------
# Create block
# ----------------------------------------------------------------------

# Block is 8 km x 8 km x 6 km
# -4 km <= x <= 4 km
# -4 km <= y <= 4 km
# -6 km <= z <= 0 km
reset
brick x 8000 y 8000 z 6000

# Translate block so the top is at z=0
volume 1 move x 0 y 0 z -3000

# ----------------------------------------------------------------------
# Create interface surfaces
# ----------------------------------------------------------------------
create planar surface with plane xplane offset 0
surface 7 name "fault_surface"
create planar surface with plane xplane offset +2000
surface 8 name "fault_surface_b"
create planar surface with plane zplane offset -2000
surface 9 name "material_interface"

# ----------------------------------------------------------------------
# Divide volumes using interface surfaces
# ----------------------------------------------------------------------
webcut volume 1 with plane surface fault_surface
webcut volume 1 with plane surface fault_surface_b
webcut volume 1 with plane surface material_interface
webcut volume 5 with plane surface material_interface
webcut volume 6 with plane surface material_interface
volume 1 name "elastic_xpos"
volume 6 name "elastic_mid"
volume 5 name "elastic_xneg"
volume 7 name "visco_xpos"
volume 9 name "visco_mid"
volume 8 name "visco_xneg"

# ----------------------------------------------------------------------
# Imprint all volumes, merging surfaces
# ----------------------------------------------------------------------
imprint all with volume all
merge all


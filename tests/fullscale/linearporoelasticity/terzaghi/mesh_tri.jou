# ----------------------------------------------------------------------
# Generate geometry
# ----------------------------------------------------------------------
#playback 'geometry.jou'

# ----------------------------------------------------------------------
# Create surface using vertices
# ----------------------------------------------------------------------

# Block is 10m x 10m
# 0 m <= x <= 10 m 
# 0 m <= y <= 10 m
reset
create vertex 0.0 0.0 0.0
create vertex 0.0 +10.0 0.0
create vertex +10.0 +10.0 0.0
create vertex +10.0 0.0 0.0
create surface vertex 1 2 3 4
delete vertex all

# ----------------------------------------------------------------------
# Set discretization size
# ----------------------------------------------------------------------
#surface all size 1.0

# ----------------------------------------------------------------------
# Generate the mesh
# ----------------------------------------------------------------------
surface all scheme trimesh
mesh surface all


# ----------------------------------------------------------------------
# Create blocks for materials
# ----------------------------------------------------------------------
block 1 surface 1
block 1 name "poroelastic"
block 1 element type tri

# ----------------------------------------------------------------------
# Create nodeset for -x edge
# ----------------------------------------------------------------------
group "x_neg" add node in curve 1
nodeset 1 group x_neg
nodeset 1 name "x_neg"

# ----------------------------------------------------------------------
# Create nodeset for +y edge
# ----------------------------------------------------------------------
group "y_pos" add node in curve  2
nodeset 2 group y_pos
nodeset 2 name "y_pos"

# ----------------------------------------------------------------------
# Create nodeset for +x edge
# ----------------------------------------------------------------------
group "x_pos" add node in curve 3
nodeset 3 group x_pos
nodeset 3 name "x_pos"

# ----------------------------------------------------------------------
# Create nodeset for -y edge Dirichlet
# ----------------------------------------------------------------------
group "y_neg_dir" add node in curve  4
nodeset 4 group y_neg_dir
nodeset 4 name "y_neg_dir"

# ----------------------------------------------------------------------
# Create nodeset for -y edge Neumann
# ----------------------------------------------------------------------
group "y_neg_neu" add node in curve  4
nodeset 5 group y_neg_neu
nodeset 5 name "y_neg_neu"
# ----------------------------------------------------------------------
# Export exodus file
# ----------------------------------------------------------------------
export mesh "mesh_tri.exo" dimension 2 overwrite



